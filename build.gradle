buildscript {
    ext {
        kotlinVersion = '1.2.21'
        springBootVersion = '1.5.10.BUILD-SNAPSHOT'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://dl.bintray.com/kotlin/exposed" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("com.github.ben-manes:gradle-versions-plugin:0.17.0")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://dl.bintray.com/kotlin/exposed" }
}

mainClassName = 'com.framework.ApplicationKt'

sourceSets {
    tools {
        kotlin {
            srcDirs += file('src/tools/kotlin')
        }
    }
}

kotlin { experimental { coroutines 'enable' } }

dependencies {

    // Kotlin
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:0.21.2")

    // Exposes information about the application i.e. heath check
    compile('org.springframework.boot:spring-boot-starter-actuator')

    // JDBC
    compile('org.apache.commons:commons-dbcp2:2.1.1')

    // Web Services
    compile('org.springframework.boot:spring-boot-starter-web')

    // GraphQL
    compile('com.graphql-java:graphql-spring-boot-starter:3.10.0')
    compile('com.graphql-java:graphql-java-tools:4.3.0')

    // Dependency used by graphql-java-tools. Forcing the version b/c kotlin 1.2 breaks this.
    compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2")

    // GSON Serialization
    compile('com.github.salomonbrys.kotson:kotson:2.5.0')
    compile('com.google.code.gson:gson:2.8.2')

    // Exposed
    compile('org.jetbrains.exposed:exposed:0.9.1')
    compile('org.jetbrains.exposed:spring-transaction:0.9.1')

    // Postgres Driver
    compile('org.postgresql:postgresql:42.1.4')
    compile('ru.yandex.qatools.embed:postgresql-embedded:2.5')

    // Tools
    toolsCompile sourceSets.main.output
    toolsCompile configurations.compile


    // Testing
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('com.h2database:h2:1.4.194')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.1"
        languageVersion = "1.2"
    }
}




